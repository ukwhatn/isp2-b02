plugins {
    id 'war'
    id 'org.gretty' version '4.1.0'
}

repositories {
    mavenCentral()
}

sourceSets {
    integrationTest {
        compileClasspath += sourceSets.main.output
        runtimeClasspath += sourceSets.main.output
    }
}

configurations {
    integrationTestImplementation.extendsFrom testImplementation
    integrationTestRuntimeOnly.extendsFrom testRuntimeOnly
}

dependencies {
    implementation group: 'org.mariadb.jdbc', name: 'mariadb-java-client', version: '3.3.1'
    implementation group: 'org.hibernate', name: 'hibernate-core', version: '6.4.1.Final', ext: 'pom'
    implementation group: 'org.json', name: 'json', version: '20231013'
    compileOnly 'jakarta.servlet:jakarta.servlet-api:5.0.0'
    testImplementation 'org.htmlunit:htmlunit:3.4.0'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.7.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
}

gretty {
    // herokuのPORTを取得
    httpPort = System.getenv("PORT") as Integer ?: 8080
    contextPath = '/isp2'
}

task integrationTest(type: Test) {
    useJUnitPlatform()
    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath
    testLogging {
        events "passed", "failed", "skipped"
    }
}

check.dependsOn integrationTest
